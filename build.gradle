buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}
apply plugin: 'groovy'

archivesBaseName = 'cloudbees-plugin'
group = 'com.manning.gia'
version = '1.0'

repositories {
    maven {
        url "https://repo1.maven.org/maven2/"
    }

    maven {
        url "https://repo.spring.io/plugins-release/"
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.cloudbees:cloudbees-api-client:1.4.0'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
}

task sourcesJar(type: Jar) {
    baseName 'cloudbees-plugin'
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    baseName 'cloudbees-plugin'
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

apply plugin: 'distribution'

distributions {
    main {
        baseName = archivesBaseName

        contents {
            from { libsDir }
        }
    }

    docs {
        baseName = "$archivesBaseName-docs"

        contents {
            from(libsDir) {
                include sourcesJar.archiveName
                include groovydocJar.archiveName
            }
        }
    }
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayUsername = 'bmuschko'
    bintrayRepository = 'gradle-plugins'
    bintrayPackage = 'gradle-cloudbees-plugin'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId 'com.manning.gia'
            artifactId 'cloudbees-plugin'
            version '0.1'
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle CloudBees plugin')
                root.appendNode('description', 'Gradle plugin for managing applications and databases on CloudBees RUN@cloud.')
                root.appendNode('inceptionYear', '2013')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'bmuschko')
                developer.appendNode('name', 'Benjamin Muschko')
                developer.appendNode('email', 'benjamin.muschko@gmail.com')
            }


        }
    }

//    repositories {
//        maven {
//            name 'Bintray'
//            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"
//
//            credentials {
//                username = bintrayUsername
//                password = bintrayApiKey
//            }
//        }
//    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']

    pkg {
        repo = 'generic'
        name = 'cloudbees-plugin'
//        userOrg = 'bintray_user'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        version {
            name = '1.0-Final'
            desc = 'Gradle Bintray Plugin 1.0 final'
            released  = new Date()
            vcsTag = '1.3.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}